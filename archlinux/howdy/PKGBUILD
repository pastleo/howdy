# Maintainer: Frank Tackitt <frank@tackitt.net>
# Maintainer: boltgolt <boltgolt@gmail.com>
# Co-Maintainer: Raymo111 <hi@raymond.li>
# Contributor: Kelley McChesney <kelley@kelleymcchesney.us>

pkgname=howdy-pastleo
pkgver=r325.cce9ea6
pkgrel=1
pkgdesc="Windows Hello for Linux"
arch=('x86_64')
url="https://github.com/boltgolt/howdy"
license=('MIT')
depends=(
	'sudo'
	'opencv'
	'hdf5'
	'pam-python'
	'python3'
	'python-pillow'
	'python-dlib'
	'python-face_recognition'
	'python-face_recognition_models'
	'python-click'
	'python-numpy'
)
makedepends=(
	'cmake'
	'pkgfile'
)
conflicts=(
	'howdy'
)
provides=(
	'howdy'
)
backup=('usr/lib/security/howdy/config.ini')
source=(
	"git://github.com/pastleo/howdy.git#branch=archlinux-sudo"
	"https://github.com/davisking/dlib-models/raw/master/dlib_face_recognition_resnet_model_v1.dat.bz2"
	"https://github.com/davisking/dlib-models/raw/master/mmod_human_face_detector.dat.bz2"
	"https://github.com/davisking/dlib-models/raw/master/shape_predictor_5_face_landmarks.dat.bz2"
)
sha256sums=('SKIP'
            'abb1f61041e434465855ce81c2bd546e830d28bcbed8d27ffbe5bb408b11553a'
            'db9e9e40f092c118d5eb3e643935b216838170793559515541c56a2b50d9fc84'
            '6e787bbebf5c9efdb793f6cd1f023230c4413306605f24f299f12869f95aa472')

pkgver() {
	cd "$srcdir/howdy"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	# Installing the proper license files and the rest of howdy
	cd "${srcdir}/howdy"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	mkdir -p "$pkgdir/usr/lib/security/howdy"
	cp -r src/* "$pkgdir/usr/lib/security/howdy"

	cp "${srcdir}/dlib_face_recognition_resnet_model_v1.dat" "$pkgdir/usr/lib/security/howdy/dlib-data/"
	cp "${srcdir}/mmod_human_face_detector.dat" "$pkgdir/usr/lib/security/howdy/dlib-data/"
	cp "${srcdir}/shape_predictor_5_face_landmarks.dat" "$pkgdir/usr/lib/security/howdy/dlib-data/"

	mkdir -p "$pkgdir/etc/sudoers.d/"
	chmod 750 "$pkgdir/etc/sudoers.d/"
	echo 'ALL ALL=(ALL) NOPASSWD:SETENV: /lib/security/howdy/compare.sh*' > "$pkgdir/etc/sudoers.d/howdy"

	mkdir -p "$pkgdir/usr/lib/security/howdy/models"
	mkdir -p "$pkgdir/usr/lib/security/howdy/snapshots"
	chmod go-rwx -R "$pkgdir/usr/lib/security/howdy"
	chmod 711 "$pkgdir/usr/lib/security/howdy"
	chmod go+r "$pkgdir/usr/lib/security/howdy/config.ini"
	chmod go+r "$pkgdir/usr/lib/security/howdy/pam.py"
	chmod u+x "$pkgdir/usr/lib/security/howdy/cli.py"

	mkdir -p "$pkgdir/usr/bin"
	ln -s /lib/security/howdy/cli.py "$pkgdir/usr/bin/howdy"
	mkdir -p "$pkgdir/usr/share/bash-completion/completions"
	cp autocomplete/howdy "$pkgdir/usr/share/bash-completion/completions/howdy"
}
